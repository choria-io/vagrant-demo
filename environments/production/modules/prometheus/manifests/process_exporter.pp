# @summary This module manages prometheus process_exporter
# @param arch
#  Architecture (amd64 or i386)
# @param bin_dir
#  Directory where binaries are located
# @param download_extension
#  Extension for the release binary archive
# @param download_url
#  Complete URL corresponding to the where the release binary archive can be downloaded
# @param download_url_base
#  Base URL for the binary archive
# @param extra_groups
#  Extra groups to add the binary user to
# @param extra_options
#  Extra options added to the startup command
# @param group
#  Group under which the binary is running
# @param init_style
#  Service startup scripts style (e.g. rc, upstart or systemd)
# @param install_method
#  Installation method: url or package (only url is supported currently)
# @param manage_group
#  Whether to create a group for or rely on external code for that
# @param manage_service
#  Should puppet manage the service? (default true)
# @param manage_user
#  Whether to create user or rely on external code for that
# @param os
#  Operating system (linux is the only one supported)
# @param package_ensure
#  If package, then use this for package ensure default 'latest'
# @param package_name
#  The binary package name - not available yet
# @param purge_config_dir
#  Purge config files no longer generated by Puppet
# @param restart_on_change
#  Should puppet restart the service on configuration change? (default true)
# @param service_enable
#  Whether to enable the service from puppet (default true)
# @param service_ensure
#  State ensured for the service (default 'running')
# @param service_name
#  Name of the process exporter service (default 'process-exporter')
# @param user
#  User which runs the service
# @param version
#  The binary release version
# @param hash_watched_processes
#  A hash of processes to monitor with the ability to pass different options
#  Don't set if you want to use only the Array version of it (watched_processes)
# @watched_processes
#  A list of processes to monitor
#  Has no effect if hash_watched_processes is set
# @example Usage with hash_watched_processes
#  class { 'prometheus::process_exporter':
#    version                => '0.6.0',
#    arch                   => 'amd64',
#    os                     => 'linux',
#    bin_dir                => '/usr/local/bin',
#    install_method         => 'url',
#    hash_watched_processes => {
#     'process_names' => [
#       {
#         'name'    => "{{.Matches}}",
#         'cmdline' => [".*process1.*"]
#       },
#       {
#         'name'    => "{{.Matches}}",
#         'cmdline' => [".*process2.*"]
#       }
#     ]
#   }
class prometheus::process_exporter(
  String $download_extension,
  Prometheus::Uri $download_url_base,
  Array $extra_groups,
  String $group,
  String $package_ensure,
  String[1] $package_name,
  String[1] $service_name,
  String $user,
  String $version,
  Stdlib::Absolutepath $config_path,
  Array $watched_processes                = [],
  Hash $hash_watched_processes            = {},
  Boolean $purge_config_dir               = true,
  Boolean $restart_on_change              = true,
  Boolean $service_enable                 = true,
  Stdlib::Ensure::Service $service_ensure = 'running',
  Prometheus::Initstyle $init_style       = $facts['service_provider'],
  String $install_method                  = $prometheus::install_method,
  Boolean $manage_group                   = true,
  Boolean $manage_service                 = true,
  Boolean $manage_user                    = true,
  String[1] $os                           = downcase($facts['kernel']),
  String $extra_options                   = '',
  String $config_mode                     = $prometheus::config_mode,
  Optional[Prometheus::Uri] $download_url = undef,
  String[1] $arch                         = $prometheus::real_arch,
  Stdlib::Absolutepath $bin_dir           = $prometheus::bin_dir,
  Boolean $export_scrape_job              = false,
  Stdlib::Port $scrape_port               = 9256,
  String[1] $scrape_job_name              = 'process',
  Optional[Hash] $scrape_job_labels       = undef,
) inherits prometheus {

  $filename = "${package_name}-${version}.${os}-${arch}.${download_extension}"
  $real_download_url = pick($download_url,"${download_url_base}/download/v${version}/${filename}")
  $notify_service = $restart_on_change ? {
    true    => Service[$service_name],
    default => undef,
  }

  if $hash_watched_processes.empty() {
    $config_path_content = template('prometheus/process-exporter.yaml.erb')
  } else {
    $config_path_content = $hash_watched_processes.to_yaml
  }

  file { $config_path:
    ensure  => 'file',
    mode    => $config_mode,
    owner   => $user,
    group   => $group,
    content => $config_path_content,
    notify  => $notify_service,
  }

  $options = "-config.path=${config_path} ${extra_options}"

  prometheus::daemon { 'process-exporter':
    install_method     => $install_method,
    version            => $version,
    download_extension => $download_extension,
    os                 => $os,
    arch               => $arch,
    real_download_url  => $real_download_url,
    bin_dir            => $bin_dir,
    notify_service     => $notify_service,
    package_name       => $package_name,
    package_ensure     => $package_ensure,
    manage_user        => $manage_user,
    user               => $user,
    extra_groups       => $extra_groups,
    group              => $group,
    manage_group       => $manage_group,
    purge              => $purge_config_dir,
    options            => $options,
    init_style         => $init_style,
    service_ensure     => $service_ensure,
    service_enable     => $service_enable,
    manage_service     => $manage_service,
    export_scrape_job  => $export_scrape_job,
    scrape_port        => $scrape_port,
    scrape_job_name    => $scrape_job_name,
    scrape_job_labels  => $scrape_job_labels,
  }
}
